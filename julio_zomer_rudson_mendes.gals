#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = espanholol
ScannerCaseSensitive = true
ScannerTable = Full
Input = String
Parser = SLR
#RegularDefinitions
NUMEROS:    [0-9]
LETRAS:     [a-zA-Z]
#Tokens
: [\s\t\n\r]*
ID : ({LETRAS}|_)[a-zA-Z0-9_]*

INTEIRO: {NUMEROS}+
REAL: {NUMEROS}*\.{NUMEROS}+
BINARIO: 0b[01]+
HEXADECIMAL: 0x[0-9A-F]+

COMENTARIO_LINHAS: "/*" ([^"*"] | [^"*/"])* "*/"	
COMENTARIO_LINHA: [/][/].*

CONTEUDO_STRING: \" [^ \" ]* \"
CONTEUDO_CHAR: '[^']'

MAIN = ID: "mayor"
IF = ID: "si"
ELSE = ID: "mas"
WHILE = ID: "mientras"
DO = ID: "hacer"
FOR = ID: "para"
CASO = ID: "caso"
BRAKE = ID: "freno"
CONTINUE = ID: "seguir"
VOID = ID: "vacio"
INT = ID: "entero"
FLOAT = ID: "flotante"
CHAR = ID: "caracter"
DOUBLE = ID: "doble"
STRING = ID: "cuerda"
BOOL = ID: "bool"
CONST = ID: "const"
UNSIGNED = ID: "nofirmado"
FUNCTION = ID: "funcion"
RETURN = ID: "devolver"
CIN = ID: "ec"
COUT = ID: "sc"

ARITMETICA: (\+|\-|\*|/|%)
SOMA = ARITMETICA: "+"
SUBTRACAO = ARITMETICA: "-"
DIVISAO = ARITMETICA: "/"
MULTIPLICACAO = ARITMETICA: "*"
RESTO_DIV_INT = ARITMETICA: "%"

ATRIBUICAO: =|\+=|\-=|\*=|/=
ATRIBUIR = ATRIBUICAO: "="
SOMA_ATRIBUIR = ATRIBUICAO: "+="
SUBTRACAO_ATRIBUIR = ATRIBUICAO: "-="
DIVISAO_ATRIBUIR = ATRIBUICAO: "/="
MULTIPLICACAO_ATRIBUIR = ATRIBUICAO: "*="

INCREMENTAR: "++"
DECREMENTAR: "--"

RELACIONAL: <|<=|>|>=|!=|==
MAIOR_QUE = RELACIONAL: ">"
MENOR_QUE = RELACIONAL: "<"
MAIOR_IGUAL_QUE = RELACIONAL: ">="
MENOR_IGUAL_QUE = RELACIONAL: "<="
DIFERENTE = RELACIONAL: "!="
IGUAL = RELACIONAL: "=="

LOGICO: &&|\|\||!
E = LOGICO: "&&"
OU = LOGICO: "||"
COMPLEMENTO = LOGICO: "!"

DELIMITADOR: ,|;|\[|\]|\(|\)|\{|\}
VIRGULA = DELIMITADOR: ","
PONTO_VIRGULA = DELIMITADOR: ";"
ABRE_COLCHETES = DELIMITADOR: "["
FECHA_COLCHETES = DELIMITADOR: "]"
ABRE_PARENTESES = DELIMITADOR: "("
FECHA_PARENTESES = DELIMITADOR: ")"
ABRE_CHAVES = DELIMITADOR: "{"
FECHA_CHAVES = DELIMITADOR: "}"

BIT_A_BIT: >>|<<|&|\||~|\^
SHIFT_RIGHT = BIT_A_BIT: ">>"
SHIFT_LEFT = BIT_A_BIT: "<<"
AND = BIT_A_BIT: "&"
OR = BIT_A_BIT: "|"
NOT = BIT_A_BIT: "~"
XOR = BIT_A_BIT: "^"
SAIDA  : "<-"
ENTRADA: "->"
#NonTerminals
<PROGRAMA>
<SUB_ROTINAS>
<MAIN>
<FUNCAO>
<PROCEDIMENTO>
<TIPO>
<DECLARACAO>
<VAR>
<VETOR>
<IO>
<COUT>
<CHAMADA_FUNCAO>
<ESCOPO>
<COMENTARIO>
<CMDS>
<CMD>
<LISTA_IDS>
<SAIDA_CONSOLE>
<RETURN>
<MULT_INSTANCIACAO>
<INSTANCIACAO>
<ATRIB_ARIT>
<PARAMETROS>
<DESVIO>
<IF>
<ELSEIF>
<ELSE>
<FOR>
<WHILE>
<OP_ATR>
<exp>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<op_rel>
<op_neg>
<op_bitwise>
<op_arit_baixa>
<op_arit_alta>
<op_arit_mais_alta>
#Grammar
<PROGRAMA> ::= <MAIN> | <SUB_ROTINAS> <MAIN>;
<MAIN> ::= INT MAIN #17 ABRE_PARENTESES #25 <PARAMETROS> FECHA_PARENTESES #24 <ESCOPO> 
	 | INT MAIN #17 ABRE_PARENTESES FECHA_PARENTESES <ESCOPO>;
<SUB_ROTINAS> ::= <COMENTARIO> | <SUB_ROTINAS> <FUNCAO> | <SUB_ROTINAS> <PROCEDIMENTO> | <FUNCAO> | <PROCEDIMENTO>;
<FUNCAO> ::= <TIPO> #1 ID #17 ABRE_PARENTESES #25 <PARAMETROS> FECHA_PARENTESES #24 <ESCOPO>
	   | <TIPO> #1 ID #17 ABRE_PARENTESES FECHA_PARENTESES <ESCOPO>;
<PROCEDIMENTO> ::= VOID #1 ID #17 ABRE_PARENTESES #25 <PARAMETROS> FECHA_PARENTESES #24 <ESCOPO>;
<PARAMETROS> ::= <TIPO> #1 ID #2 | <PARAMETROS> VIRGULA <TIPO> #1 ID #2;

<MULT_INSTANCIACAO> ::= <MULT_INSTANCIACAO> VIRGULA <DECLARACAO> | <DECLARACAO>;
<DECLARACAO> ::= <VAR> #24 | <VAR> ATRIBUIR #27 <exp> #4;
<VAR> ::= ID #2 | <VETOR>;
<VETOR> ::= ID #3 ABRE_COLCHETES FECHA_COLCHETES | ID #3 ABRE_COLCHETES #15 <exp> FECHA_COLCHETES #16;
<INSTANCIACAO> ::= <TIPO> #1 <MULT_INSTANCIACAO>;
<TIPO> ::= INT | FLOAT | CHAR | DOUBLE | STRING | BOOL;
<ESCOPO> ::= ABRE_CHAVES #18 <CMDS> FECHA_CHAVES #19 | ABRE_CHAVES FECHA_CHAVES;
<DESVIO> ::= <IF> | <IF> <ELSEIF>;
<IF> ::= IF ABRE_PARENTESES <exp> FECHA_PARENTESES #20 <ESCOPO>;
<ELSEIF> ::= ELSE <IF> <ELSEIF> | ELSE <IF> | <ELSE>;
<ELSE> ::= ELSE <ESCOPO>;
<FOR> ::= FOR #22 ABRE_PARENTESES <INSTANCIACAO> PONTO_VIRGULA <exp> #23 PONTO_VIRGULA <OP_ATR> FECHA_PARENTESES <ESCOPO>;
<WHILE> ::= WHILE ABRE_PARENTESES <exp> FECHA_PARENTESES #20 <ESCOPO> 
	  | DO <ESCOPO> WHILE ABRE_PARENTESES <exp> #20 FECHA_PARENTESES PONTO_VIRGULA;
<RETURN> ::= RETURN <exp> PONTO_VIRGULA;

<CMDS> ::= <CMDS> <CMD> | <CMD>;
<CMD> ::= <INSTANCIACAO> PONTO_VIRGULA #14 | <OP_ATR> PONTO_VIRGULA
	| <DESVIO> | <FOR> | <WHILE> | <RETURN> | <CHAMADA_FUNCAO> PONTO_VIRGULA | <IO> | <COMENTARIO>;
<COMENTARIO> ::= COMENTARIO_LINHA | COMENTARIO_LINHAS;

<IO> ::= CIN ENTRADA ID PONTO_VIRGULA | COUT <COUT> PONTO_VIRGULA;
<COUT> ::= <COUT> SAIDA <exp> | SAIDA <exp>;

<LISTA_IDS> ::= <LISTA_IDS> VIRGULA <exp> | <exp>;
<CHAMADA_FUNCAO> ::= ID ABRE_PARENTESES <LISTA_IDS> FECHA_PARENTESES | ID ABRE_PARENTESES FECHA_PARENTESES;
<OP_ATR> ::= ID <ATRIB_ARIT> <exp> | ID INCREMENTAR | ID DECREMENTAR | INCREMENTAR ID | DECREMENTAR ID | <DECLARACAO>;
<ATRIB_ARIT> ::= SOMA_ATRIBUIR | SUBTRACAO_ATRIBUIR | DIVISAO_ATRIBUIR | MULTIPLICACAO_ATRIBUIR;
<exp>  ::= <exp> OU <exp1> | <exp1>;
<exp1> ::= <exp1> E <exp2> | <exp2>;
<exp2> ::= <exp2> OR <exp3> | <exp3>;
<exp3> ::= <exp3> XOR <exp4> | <exp4>;
<exp4> ::= <exp4> AND <exp5> | <exp5>;
<exp5> ::= <exp5> <op_rel> #21 <exp6> | <exp6>;
<exp6> ::= <exp6> <op_bitwise> <exp7> | <exp7>;
<exp7> ::= <exp7> <op_arit_baixa> <exp8> | <exp8>;
<exp8> ::= <exp8> <op_arit_alta> <exp9> | <exp9> ;
<exp9> ::= <op_neg> <exp10> | <exp10>;
<exp10> ::= INTEIRO #9 | REAL #10 | ID #13 | ABRE_PARENTESES <exp> FECHA_PARENTESES | CONTEUDO_STRING #12 | CONTEUDO_CHAR #11 
	| ID #13 ABRE_PARENTESES #15 <LISTA_IDS> FECHA_PARENTESES #16 | ID #13 ABRE_PARENTESES FECHA_PARENTESES
	| ID #26 ABRE_COLCHETES FECHA_COLCHETES | ID #26 ABRE_COLCHETES #15 <exp> FECHA_COLCHETES #16;
<op_rel> ::= MAIOR_QUE | MENOR_QUE | MAIOR_IGUAL_QUE | MENOR_IGUAL_QUE | ATRIBUIR | DIFERENTE; 
<op_neg> ::= SUBTRACAO | NOT | COMPLEMENTO;
<op_bitwise> ::= SHIFT_LEFT #28 | SHIFT_RIGHT #29;
<op_arit_baixa> ::= SOMA #5 | SUBTRACAO #6;
<op_arit_alta> ::= MULTIPLICACAO #7 | DIVISAO #8 | RESTO_DIV_INT;
